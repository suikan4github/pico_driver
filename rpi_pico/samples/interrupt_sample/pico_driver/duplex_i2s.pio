;
;  RP2040 / RP2035 PIO program for the duplex I2S transfer in slave mode. 
;
;
; The I2S signal wave form is depicted below : 
;
;                  ┐　┌─┐　┌─┐　┌─┐　┌─┐　
;             BCLK └─┘　└─┘　└─┘　└─┘　└─
;                  ┐
;               WS └─────────────────
;
;         BIT SLOT │　００│　３１│　３０　│　２９│　
;
; The rising edge of the BCLK is the sampling edge of the I2S. 
; So we can call the period from a falling edge to the next falling
; edge as a "BIT SLOT". 
;
; Per definition of I2S standard, the first bit slot after the
; falling edge of the WS is the last bit slot (00) of the previous 
; right channel. 
;
; In the program below, we capture the falling edge of the WS as 
; a begining of the left data, and skip the first bit slot. And then, 
; start the left channel data transfer. 
;

.pio_version 0          ; only requires PIO version 0

.program duplex_i2s
	wait 1 pin 2        ; Waiting for 0 of WS (L ch). 
	wait 0 pin 2        ; Capture falling edge of WS(Sync to the start of L ch). 
	wait 0 pin 1        ; Waiting for 0 of BCLK(first half of bit slot 00). 
	wait 1 pin 1        ; Capture rising edge of BCLK( Sync to the second half of bit slot 00).
.wrap_target                ; Begining of stereo data transfer.
                        ; Transfer Left ch
	set x,31            ; Loop 32 times ( 32bit transfer )
left_loop:                  ; Loop here
        wait 0 pin 1        ; Capture falling edge of BCLK(Sync to the first half of BCLK ). 
        out pins, 1         ; Output 1 bit to DAC. 
        wait 1 pin 1        ; Capture riging edge of BCLK(Sync to the Sampling edge of BCLK). 
        in pins, 1          ; Input 1 bit from ADC.
	jmp x-- left_loop   ; repeat until x == 0 with post decrement. 
                        ; Transfer Right ch
	set x,31            ; Loop 32 times ( 32bit transfer )
right_loop:                 ; Loop here
        wait 0 pin 1        ; Capture falling edge of BCLK(Sync to the first half of BCLK ). 
        out pins, 1         ; Output 1 bit to DAC. 
        wait 1 pin 1        ; Capture riging edge of BCLK(Sync to the Sampling edge of BCLK). 
        in pins, 1          ; Input 1 bit from ADC.
	jmp x-- right_loop  ; repeat until x == 0 with post decrement. 
.wrap

; This program takes 4 cycles for the second half of a bit slot. 
; This happens when program wraps. So, for the entire clock cycle, 
; we need 8 cycle. 
;
; For the 96kHz I2S stereo ( 32bit word), the BCLK is 6MHz. As a result, 
; we need 48MHz for PIO clock. 
